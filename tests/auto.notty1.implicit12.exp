#!/usr/bin/env expect

if { [info exists ::env(MUTE_TOP)] } {
    set MUTE_TOP $::env(MUTE_TOP)
}
if { ! [info exists MUTE_TOP]} {
    set MUTE_TOP [exec git rev-parse --show-toplevel --show-superproject-working-tree 2>/dev/null]
}
if { ! [info exists MUTE_TOP]} {
    set MUTE_TOP "[file dirname $argv0]/.."
}

puts "MUTE_TOP=$MUTE_TOP"
set MUTE "$MUTE_TOP/mute"
set CHAT "$MUTE_TOP/tests/chat"

log_user 0
set timeout 5

set tmpfile [exec mktemp]
set chatpid [spawn sh -c "exec $CHAT -w 5 >$tmpfile"]
set chat_spawn_id $spawn_id
puts "chat pid=$chatpid"
expect "stderr"

system $MUTE -dd $chatpid
puts "mute complete, restarting chat $chatpid"
system kill -USR1 $chatpid

set spawn_id $chat_spawn_id
expect {
    timeout        {puts "timed out"; exit 1}
    "stdout"       {puts "saw stdout"; exit 2}
    "stderr"       {puts "saw stderr"; exit 3}
    "saved stdout" {puts "saw saved stdout"; exit 4}
    eof            {puts "correctly saw no more output"}
}

spawn -open [open $tmpfile]
expect {
    "stdout" {puts "stdout redirection was successful"}
    eof      {puts "stdout was not redirected"; system rm $tmpfile; exit 5}
}
for {set i 1} {$i <= 5} {incr i} {
    expect {
        "stdout $i" {}
        eof         {puts "stdout counts are missing"; system rm $tmpfile; exit 6}
    }
}
expect {
    eof {puts "stdout output was correct"}
}
system rm $tmpfile
